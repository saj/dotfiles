__zshenv_python3_user_base() {
  python3 -c 'import site; print(site.USER_BASE)'
}

# Taken from the output of 'pyenv init -' with modifications:
#   - Do not rehash on init.  This is horrifically slow (~100ms).
#
__zshenv_pyenv_init() {
  local -r pyenv_base="${1:?}"

  __zshenv_lib_prepend_path 'PATH' "${pyenv_base}/shims"
  export PYENV_SHELL=zsh

  pyenv() {
    local command
    command="${1:-}"
    if [ "$#" -gt 0 ]; then
      shift
    fi

    case "$command" in
    rehash|shell)
      eval "$(pyenv "sh-$command" "$@")";;
    *)
      command pyenv "$command" "$@";;
    esac
  }
}

() {
  if command -v python3 >/dev/null; then
    __zshenv_lib_prepend_path 'PATH' "$(__zshenv_memoise __zshenv_python3_user_base)/bin"
  fi

  local -r pyenv_base=~/.pyenv
  if [[ -d "${pyenv_base}" ]]; then
    export PYENV_ROOT="${pyenv_base}"
    __zshenv_lib_prepend_path 'PATH' "${pyenv_base}/bin"
    __zshenv_pyenv_init "${pyenv_base}"
  fi
}

unfunction __zshenv_python3_user_base
unfunction __zshenv_pyenv_init

# vim:ft=zsh
