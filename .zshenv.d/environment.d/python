__zshenv_python_user_base() {
  python -c 'import site; print site.USER_BASE' 2>/dev/null
}

__zshenv_python() {
  local -r user_base="$(__zshenv_memoise __zshenv_python_user_base)"

  if [[ -n "${user_base}" && -d "${user_base}" ]]; then
    __zshenv_lib_prepend_path 'PATH' "${user_base}/bin"
  fi
}

__zshenv_python
unfunction __zshenv_python_user_base
unfunction __zshenv_python

__zshenv_pyenv_init() {
  pyenv init - 2>/dev/null
}

__zshenv_pyenv() {
  local -r pyenv_base="${HOME}/.pyenv"

  if [[ -d "${pyenv_base}" ]]; then
    export PYENV_ROOT="${pyenv_base}"
    __zshenv_lib_prepend_path 'PATH' "${pyenv_base}/bin"
    eval "$(__zshenv_memoise __zshenv_pyenv_init)"
  fi
}

__zshenv_pyenv
unfunction __zshenv_pyenv_init
unfunction __zshenv_pyenv

# vim:ft=zsh
