__zshenv_python_user_base() {
  python -c 'import site; print site.USER_BASE' 2>/dev/null
}

() {
  local -r user_base="$(__zshenv_memoise __zshenv_python_user_base)"

  if [[ -n "${user_base}" && -d "${user_base}" ]]; then
    __zshenv_lib_prepend_path 'PATH' "${user_base}/bin"
  fi
}

unfunction __zshenv_python_user_base

# Taken from the output of 'pyenv init -' with modifications:
#   - Do not rehash on init.  This is horrifically slow (~100ms).
#
__zshenv_pyenv_init() {
  local -r pyenv_base="${1:?}"

  __zshenv_lib_prepend_path 'PATH' "${pyenv_base}/shims"
  export PYENV_SHELL=zsh

  pyenv() {
    local command
    command="${1:-}"
    if [ "$#" -gt 0 ]; then
      shift
    fi

    case "$command" in
    rehash|shell)
      eval "$(pyenv "sh-$command" "$@")";;
    *)
      command pyenv "$command" "$@";;
    esac
  }
}

() {
  local -r pyenv_base=~/.pyenv

  if [[ -d "${pyenv_base}" ]]; then
    export PYENV_ROOT="${pyenv_base}"
    __zshenv_lib_prepend_path 'PATH' "${pyenv_base}/bin"
    __zshenv_pyenv_init "${pyenv_base}"
  fi
}

unfunction __zshenv_pyenv_init

# vim:ft=zsh
