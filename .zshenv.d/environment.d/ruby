# Taken from the output of 'rbenv init -' with modifications:
#   - Do not rehash on init.  This is slow (~60ms).
#   - Drop shell completions.  I don't need them.
#
__zshenv_rbenv_init() {
  local -r rbenv_base="${1:?}"

  __zshenv_lib_prepend_path 'PATH' "${rbenv_base}/shims"
  export RBENV_SHELL=zsh

  rbenv() {
    local command
    command="${1:-}"
    if [ "$#" -gt 0 ]; then
      shift
    fi

    case "$command" in
    rehash|shell)
      eval "$(rbenv "sh-$command" "$@")";;
    *)
      command rbenv "$command" "$@";;
    esac
  }
}

__zshenv_insert_rbenv_path() {
  local -r rbenv_base=~/.rbenv
  [[ -d "${rbenv_base}" ]] || return 0
  __zshenv_lib_prepend_path 'PATH' "${rbenv_base}/bin"
  __zshenv_rbenv_init "${rbenv_base}"
}

() {
  __zshenv_insert_rbenv_path
}

unfunction __zshenv_insert_rbenv_path
unfunction __zshenv_rbenv_init

# vim:ft=zsh
